group 'org.no_creativity.anvergen'

def getLastTag() {
    def cmd = "git for-each-ref --sort=-taggerdate --count=1 --format %(tag) refs/tags"
    def process = cmd.execute()
    process.waitFor()
    String tag = process.getText().trim()
    if (tag.isEmpty()) {
        tag = "0.0"
    }
    return tag
}

def tag = getLastTag()

def getCommitCount(String tag) {
    String cmd
    if ("0.0".equals(tag)) {
        cmd = "git rev-list --count HEAD"
    } else {
        cmd = "git rev-list --count ${tag}...HEAD"
    }
    def process = cmd.execute()
    process.waitFor()
    return process.getText().toInteger()
}

version "${tag}.${getCommitCount(tag)}"

apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'junit:junit:4.12'
}

task writePom << {
    pom {
        project {
            inceptionYear '2016'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

build.dependsOn("writePom")
